function(april_get_platform_key OUT_VAR)
    if(WIN32)
        set(PLATFORM_KEY "windows")
    elseif(APPLE)
        set(PLATFORM_KEY "macos")
    elseif(UNIX)
        set(PLATFORM_KEY "linux")
    else()
        set(PLATFORM_KEY "unknown")
    endif()

    set(${OUT_VAR} "${PLATFORM_KEY}" PARENT_SCOPE)
endfunction()

function(april_platform_select_list OUT_VAR WINDOWS_LIST LINUX_LIST MACOS_LIST)
    april_get_platform_key(PLATFORM_KEY)

    if(PLATFORM_KEY STREQUAL "windows")
        set(${OUT_VAR} "${WINDOWS_LIST}" PARENT_SCOPE)
    elseif(PLATFORM_KEY STREQUAL "linux")
        set(${OUT_VAR} "${LINUX_LIST}" PARENT_SCOPE)
    elseif(PLATFORM_KEY STREQUAL "macos")
        set(${OUT_VAR} "${MACOS_LIST}" PARENT_SCOPE)
    else()
        set(${OUT_VAR} "" PARENT_SCOPE)
        message(WARNING "[Platform] Unknown platform. Skipping platform-specific settings.")
    endif()
endfunction()

function(april_manifest_normalize_platform VALUE OUT_VAR)
    string(TOLOWER "${VALUE}" PLATFORM_LOWER)

    if(PLATFORM_LOWER STREQUAL "windows" OR PLATFORM_LOWER STREQUAL "win32")
        set(${OUT_VAR} "windows" PARENT_SCOPE)
    elseif(PLATFORM_LOWER STREQUAL "linux")
        set(${OUT_VAR} "linux" PARENT_SCOPE)
    elseif(PLATFORM_LOWER STREQUAL "mac" OR PLATFORM_LOWER STREQUAL "osx" OR PLATFORM_LOWER STREQUAL "macos" OR PLATFORM_LOWER STREQUAL "darwin")
        set(${OUT_VAR} "macos" PARENT_SCOPE)
    else()
        set(${OUT_VAR} "" PARENT_SCOPE)
    endif()
endfunction()
