function(april_manifest_parse_bool VALUE OUT_VAR)
    string(TOLOWER "${VALUE}" VALUE_LOWER)

    if(VALUE_LOWER STREQUAL "1" OR VALUE_LOWER STREQUAL "true" OR VALUE_LOWER STREQUAL "yes" OR VALUE_LOWER STREQUAL "on")
        set(${OUT_VAR} ON PARENT_SCOPE)
    else()
        set(${OUT_VAR} OFF PARENT_SCOPE)
    endif()
endfunction()

function(april_manifest_normalize_module_type VALUE OUT_VAR)
    set(TYPE "STATIC")

    if(NOT VALUE STREQUAL "")
        string(TOUPPER "${VALUE}" TYPE_UPPER)
        if(TYPE_UPPER STREQUAL "LIBRARY")
            set(TYPE "STATIC")
        elseif(TYPE_UPPER STREQUAL "STATIC" OR TYPE_UPPER STREQUAL "SHARED")
            set(TYPE "${TYPE_UPPER}")
        else()
            message(WARNING "[Manifest] Unknown module type '${VALUE}'. Defaulting to STATIC.")
        endif()
    endif()

    set(${OUT_VAR} "${TYPE}" PARENT_SCOPE)
endfunction()

function(april_manifest_append_list OUT_LIST_VAR VALUE)
    set(LIST_VALUES "${${OUT_LIST_VAR}}")

    string(REPLACE "," ";" VALUE_LIST "${VALUE}")
    string(REPLACE ";" ";" VALUE_LIST "${VALUE_LIST}")

    foreach(ITEM IN LISTS VALUE_LIST)
        string(STRIP "${ITEM}" ITEM)
        if(NOT ITEM STREQUAL "")
            list(APPEND LIST_VALUES "${ITEM}")
        endif()
    endforeach()

    set(${OUT_LIST_VAR} "${LIST_VALUES}" PARENT_SCOPE)
endfunction()

function(april_manifest_append_deps OUT_LIST_VAR VALUE)
    set(LIST_VALUES "${${OUT_LIST_VAR}}")

    string(REGEX REPLACE "[, \t]+" ";" VALUE_LIST "${VALUE}")

    foreach(ITEM IN LISTS VALUE_LIST)
        string(STRIP "${ITEM}" ITEM)
        if(NOT ITEM STREQUAL "")
            list(APPEND LIST_VALUES "${ITEM}")
        endif()
    endforeach()

    set(${OUT_LIST_VAR} "${LIST_VALUES}" PARENT_SCOPE)
endfunction()

function(april_manifest_append_copy_dirs OUT_LIST_VAR VALUE)
    set(LIST_VALUES "${${OUT_LIST_VAR}}")
    set(PAIRS "${VALUE}")

    string(STRIP "${PAIRS}" PAIRS)
    string(REGEX REPLACE "^\\{" "" PAIRS "${PAIRS}")
    string(REGEX REPLACE "\\}$" "" PAIRS "${PAIRS}")
    string(REPLACE "," ";" PAIRS "${PAIRS}")

    foreach(PAIR IN LISTS PAIRS)
        string(STRIP "${PAIR}" PAIR)
        if(PAIR STREQUAL "")
            continue()
        endif()

        if(NOT PAIR MATCHES "^(.*):(.*)$")
            continue()
        endif()

        set(SRC "${CMAKE_MATCH_1}")
        set(DST "${CMAKE_MATCH_2}")

        string(STRIP "${SRC}" SRC)
        string(STRIP "${DST}" DST)
        string(REGEX REPLACE "^\"(.*)\"$" "\\1" SRC "${SRC}")
        string(REGEX REPLACE "^\"(.*)\"$" "\\1" DST "${DST}")

        if(NOT SRC STREQUAL "" AND NOT DST STREQUAL "")
            list(APPEND LIST_VALUES "${SRC}|${DST}")
        endif()
    endforeach()

    set(${OUT_LIST_VAR} "${LIST_VALUES}" PARENT_SCOPE)
endfunction()

function(april_parse_manifest FILE_PATH OUT_PREFIX)
    if(NOT EXISTS "${FILE_PATH}")
        return()
    endif()

    file(STRINGS "${FILE_PATH}" LINES)

    set(VERSION "0.0.0")
    set(MODULE_NAME "")
    set(MODULE_TYPE "")
    set(BUILD_PCH "")
    set(BUILD_UNITY "")
    set(DEPS_PUBLIC "")
    set(DEPS_PRIVATE "")
    set(DEPS_PUBLIC_WINDOWS "")
    set(DEPS_PRIVATE_WINDOWS "")
    set(DEPS_PUBLIC_LINUX "")
    set(DEPS_PRIVATE_LINUX "")
    set(DEPS_PUBLIC_MACOS "")
    set(DEPS_PRIVATE_MACOS "")
    set(DEFINES_PUBLIC "")
    set(DEFINES_PRIVATE "")
    set(DEFINES_PUBLIC_WINDOWS "")
    set(DEFINES_PRIVATE_WINDOWS "")
    set(DEFINES_PUBLIC_LINUX "")
    set(DEFINES_PRIVATE_LINUX "")
    set(DEFINES_PUBLIC_MACOS "")
    set(DEFINES_PRIVATE_MACOS "")
    set(CODEGEN_CMD "")
    set(CODEGEN_OUTPUT "")
    set(RES_COPY_DIR "")

    set(USE_NEW_FORMAT FALSE)
    foreach(LINE IN LISTS LINES)
        string(STRIP "${LINE}" LINE)
        if(LINE MATCHES "^\\[.*\\]$")
            set(USE_NEW_FORMAT TRUE)
            break()
        endif()
    endforeach()

    if(NOT USE_NEW_FORMAT)
        message(WARNING "[Manifest] ${FILE_PATH} uses legacy format. This format is no longer supported.")
        return()
    endif()

    set(CURRENT_SECTION "")

    foreach(LINE IN LISTS LINES)
        string(STRIP "${LINE}" LINE)

        if(LINE STREQUAL "" OR LINE MATCHES "^#")
            continue()
        endif()

        if(LINE MATCHES "^\\[(.+)\\]$")
            set(CURRENT_SECTION "${CMAKE_MATCH_1}")
            string(TOLOWER "${CURRENT_SECTION}" CURRENT_SECTION)
            continue()
        endif()

        if(NOT LINE MATCHES "^([^=]+)=(.*)$")
            continue()
        endif()

        set(KEY "${CMAKE_MATCH_1}")
        set(VALUE "${CMAKE_MATCH_2}")
        string(STRIP "${KEY}" KEY)
        string(STRIP "${VALUE}" VALUE)
        string(TOLOWER "${KEY}" KEY_LOWER)

        set(SECTION_BASE "${CURRENT_SECTION}")
        set(SECTION_PLATFORM "")

        if(CURRENT_SECTION MATCHES "^(dependencies|defines)\\.(.+)$")
            set(SECTION_BASE "${CMAKE_MATCH_1}")
            april_manifest_normalize_platform("${CMAKE_MATCH_2}" SECTION_PLATFORM)
        endif()

        if(SECTION_BASE STREQUAL "module")
            if(KEY_LOWER STREQUAL "name")
                set(MODULE_NAME "${VALUE}")
            elseif(KEY_LOWER STREQUAL "type")
                set(MODULE_TYPE "${VALUE}")
            elseif(KEY_LOWER STREQUAL "version")
                set(VERSION "${VALUE}")
            endif()
        elseif(SECTION_BASE STREQUAL "build")
            if(KEY_LOWER STREQUAL "pch")
                set(BUILD_PCH "${VALUE}")
            elseif(KEY_LOWER STREQUAL "unity_build" OR KEY_LOWER STREQUAL "unity")
                april_manifest_parse_bool("${VALUE}" BUILD_UNITY)
            endif()
        elseif(SECTION_BASE STREQUAL "dependencies")
            if(SECTION_PLATFORM STREQUAL "")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_deps(DEPS_PUBLIC "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_deps(DEPS_PRIVATE "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "windows")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_deps(DEPS_PUBLIC_WINDOWS "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_deps(DEPS_PRIVATE_WINDOWS "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "linux")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_deps(DEPS_PUBLIC_LINUX "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_deps(DEPS_PRIVATE_LINUX "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "macos")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_deps(DEPS_PUBLIC_MACOS "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_deps(DEPS_PRIVATE_MACOS "${VALUE}")
                endif()
            endif()
        elseif(SECTION_BASE STREQUAL "defines")
            if(SECTION_PLATFORM STREQUAL "")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_list(DEFINES_PUBLIC "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_list(DEFINES_PRIVATE "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "windows")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_list(DEFINES_PUBLIC_WINDOWS "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_list(DEFINES_PRIVATE_WINDOWS "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "linux")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_list(DEFINES_PUBLIC_LINUX "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_list(DEFINES_PRIVATE_LINUX "${VALUE}")
                endif()
            elseif(SECTION_PLATFORM STREQUAL "macos")
                if(KEY_LOWER STREQUAL "public")
                    april_manifest_append_list(DEFINES_PUBLIC_MACOS "${VALUE}")
                elseif(KEY_LOWER STREQUAL "private")
                    april_manifest_append_list(DEFINES_PRIVATE_MACOS "${VALUE}")
                endif()
            endif()
        elseif(SECTION_BASE STREQUAL "codegen")
            if(KEY_LOWER STREQUAL "cmd")
                set(CODEGEN_CMD "${VALUE}")
            elseif(KEY_LOWER STREQUAL "output")
                set(CODEGEN_OUTPUT "${VALUE}")
            endif()
        elseif(SECTION_BASE STREQUAL "resources")
            if(KEY_LOWER STREQUAL "copy_dir")
                april_manifest_append_copy_dirs(RES_COPY_DIR "${VALUE}")
            endif()
        endif()
    endforeach()

    set(${OUT_PREFIX}_VERSION "${VERSION}" PARENT_SCOPE)
    set(${OUT_PREFIX}_MODULE_NAME "${MODULE_NAME}" PARENT_SCOPE)
    set(${OUT_PREFIX}_MODULE_TYPE "${MODULE_TYPE}" PARENT_SCOPE)
    set(${OUT_PREFIX}_BUILD_PCH "${BUILD_PCH}" PARENT_SCOPE)
    set(${OUT_PREFIX}_BUILD_UNITY "${BUILD_UNITY}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PUBLIC "${DEPS_PUBLIC}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PRIVATE "${DEPS_PRIVATE}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PUBLIC_WINDOWS "${DEPS_PUBLIC_WINDOWS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PRIVATE_WINDOWS "${DEPS_PRIVATE_WINDOWS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PUBLIC_LINUX "${DEPS_PUBLIC_LINUX}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PRIVATE_LINUX "${DEPS_PRIVATE_LINUX}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PUBLIC_MACOS "${DEPS_PUBLIC_MACOS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEPS_PRIVATE_MACOS "${DEPS_PRIVATE_MACOS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PUBLIC "${DEFINES_PUBLIC}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PRIVATE "${DEFINES_PRIVATE}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PUBLIC_WINDOWS "${DEFINES_PUBLIC_WINDOWS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PRIVATE_WINDOWS "${DEFINES_PRIVATE_WINDOWS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PUBLIC_LINUX "${DEFINES_PUBLIC_LINUX}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PRIVATE_LINUX "${DEFINES_PRIVATE_LINUX}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PUBLIC_MACOS "${DEFINES_PUBLIC_MACOS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_DEFINES_PRIVATE_MACOS "${DEFINES_PRIVATE_MACOS}" PARENT_SCOPE)
    set(${OUT_PREFIX}_CODEGEN_CMD "${CODEGEN_CMD}" PARENT_SCOPE)
    set(${OUT_PREFIX}_CODEGEN_OUTPUT "${CODEGEN_OUTPUT}" PARENT_SCOPE)
    set(${OUT_PREFIX}_RES_COPY_DIR "${RES_COPY_DIR}" PARENT_SCOPE)
endfunction()
